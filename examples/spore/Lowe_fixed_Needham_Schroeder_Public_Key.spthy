theory Lowe_fixed_Needham_Shroeder_Public_Key begin

section{* Needham-Schroeder-Lowe Public-Key Protocol *}

text{*
  Modelled after SPORE.

  Notable differences:
    1. We use explicit global constants instead of implicit typing to discern
       the different messages.
    
    2. We model a single certificate-authority (CA) server role, which is used
       to answer the certificate requests from both A and B.

    2. We prove non-injective synchronization, which is stronger than the
       entity authentication property stated in SPORE as the requirement on
       this protocol.

*}


protocol NSLPK
{
  // the certificate-authority server.
  ca1. -> S: A, B 
  ca2. <- S: S, sign{'ca2', pk(B), B}pk(S) 

  ca1. I -> : I, R
  ca2. I <- : S, sign{'ca2', pkR,    R}pk(S)

  1. I ->  : I, {'1', ni, I}pkR   
       -> R: I, {'1', ni, I}pk(R)

  ca1. R -> : R, I
  ca2. R <- : S, sign{'ca2', pkI,  I}pk(S)
     
  2.   <- R: {'2', ni, nr, R}pkI
     I <-  : {'2', ni, nr, R}pk(I)

  3. I ->  : {'3', nr}pkR
       -> R: {'3', nr}pk(R)
}


subsection{* Assumptions *}

text{* 
  Initiators as well as responders talk to an uncompromised server. 
*}

axiom (of NSLPK) I_uncompromised_S:
  premises
    "role(1) = I"
  imply "uncompromised(S#1)"

axiom (of NSLPK) R_uncompromised_S:
  premises
    "role(1) = R"
  imply "uncompromised(S#1)"


subsection{* Security Properties *}

property (of NSLPK) I_pkR_auth:
  premises
    "role(1) = I"
    "step(1, I_ca2)"
  imply
    "pkR#1 = pk(R#1)"

property (of NSLPK) R_pkI_auth:
  premises
    "role(1) = R"
    "step(1, R_ca2)"
  imply
    "pkI#1 = pk(I#1)"

properties (of NSLPK)
  I_ni_secrecy: secret(I, -, ni, {I,R})
  R_nr_secrecy: secret(R, -, nr, {I,R})
  I_nr_secrecy: secret(I, 2, nr, {I,R})
  R_ni_secrecy: secret(R, 3, ni, {I,R})


property (of NSLPK) I_noninjective_synch:
  premises
    "role(1) = I"
    "step(1, I_2)"
    "uncompromised(I#1, R#1)"
  imply a thread 2 such that
    "  role(2) = R
     & I#1  = I#2
     & R#1  = R#2
     & ni#1 = ni#2
     & nr#1 = nr#2 
     
     & St(1, I_1) < St(2, R_1)
     & St(2, R_2) < St(1, I_2)
     & St(1, I_1) < St(1, I_2)
     & St(2, R_1) < St(2, R_2)
    "

property (of NSLPK) R_noninjective_synch:
  premises
    "role(2) = R"
    "step(2, R_3)"
    "uncompromised(I#2, R#2)"
  imply a thread 1 such that
    "  role(1) = I
     & I#1  = I#2
     & R#1  = R#2
     & ni#1 = ni#2
     & nr#1 = nr#2

     & St(1, I_1) < St(2, R_1)
     & St(2, R_2) < St(1, I_2)
     & St(1, I_3) < St(2, R_3)
     & St(1, I_1) < St(1, I_2) < St(1, I_3)
     & St(2, R_1) < St(2, R_2) < St(2, R_3)
    "

end

